(ns manifold-bot.strategies 
  (:require [manifold-bot.api :as api]
            [manifold-bot.config :as config]))

(def strategy-registry (atom {}))

(defmacro defstrategy
  "Defines a new strategy and registers it if enabled.
   
   Parameters:
   - name: The name of the strategy.
   - description: A string describing the strategy.
   - get-search-params: A function that returns market search parameters for the strategy.
   - get-trades: A function that generates trades based on market data.
   
   Returns:
   The defined strategy map."
  [name & {:keys [description get-search-params get-trades]}]
  (let [enabled (:enabled (config/config-for-strategy name))
        strategy {:name (str name)
                  :description description
                  :get-search-params get-search-params
                  :get-trades (if enabled
                                get-trades
                                `(fn [markets#]
                                   (map #(assoc % :dry-run true)
                                        (~get-trades markets#))))}
        sym (symbol (str (clojure.string/lower-case (str name)) "-strategy"))]
    `(do
       (def ~sym ~strategy)
       (when ~enabled
         (swap! strategy-registry assoc '~name ~strategy))
       ~strategy)))

(defn get-search-params
  "Retrieves market search parameters for a given strategy.
   
   Parameters:
   - strategy: A map containing strategy details.
   
   Returns:
   A map of market search parameters for the strategy."
  [strategy]
  ((:get-search-params strategy) (config/config-for-strategy (:name strategy))))

(defn get-trades
  "Generates trades for a given strategy based on market data.
   
   Parameters:
   - strategy: A map containing strategy details.
   - markets: A list of markets to generate trades for.
   
   Returns:
   A list of trades generated by the strategy."
  [strategy markets]
  ((:get-trades strategy) (config/config-for-strategy (:name strategy)) markets))

(defstrategy Null
  :description "Do nothing"
  :get-search-params {:limit 1}
  :get-trades (fn [config markets] []))

(defstrategy CoinFlip
  :description "Market make in @Traveel's daily coinflip markets"
  :get-search-params (fn [config]
                       {:term "Daily coinflip"
                        :creatorId "gRmM27eQDEVTEjM1q6Yzc7abJT93" ; @Traveel
                        :filter "open"
                        :contractType "BINARY"})
  :get-trades (fn [config markets]
                (mapcat (fn [{:keys [id]}]
                          (let [positions (api/get-my-positions id)
                                orders (api/get-my-open-orders id)
                                should-bet? (fn [outcome]
                                              (not (or
                                                    ((keyword outcome) positions)
                                                    (some #(= (:outcome %) outcome) orders))))
                                duration-seconds (* 30 24 60 60)]
                            (remove nil?
                                    [(when (should-bet? "YES")
                                       {:market-id id
                                        :amount (:size config)
                                        :outcome "YES"
                                        :limit 0.47
                                        :duration-seconds duration-seconds})
                                     (when (should-bet? "NO")
                                       {:market-id id
                                        :amount (:size config)
                                        :outcome "NO"
                                        :limit 0.53
                                        :duration-seconds duration-seconds})])))
                        markets)))

(defn get-strategies
  "Retrieves all enabled strategies from the strategy registry.
   
   Returns:
   A list of all enabled strategy maps."
  []
  (vals @strategy-registry))