(ns manifold-bot.strategies 
  (:require [clojure.string :as string]
            [manifold-bot.api :as api]
            [manifold-bot.config :as config]))

(def strategy-registry (atom {}))

; Public interface

(defmacro defstrategy
  "Defines a new strategy and registers it if enabled.
   
   Parameters:
   - name: The name of the strategy.
   - description: A string describing the strategy.
   - get-search-params: A function that returns market search parameters for the strategy, either as a single map or a list of maps.
   - get-trades: A function that generates trades based on market data.
   
   Returns:
   The defined strategy map."
  [name & {:keys [description get-search-params get-trades]}]
  (let [enabled (:enabled (config/config-for-strategy name))
        strategy {:name (str name)
                  :description description
                  :get-search-params get-search-params
                  :get-trades (if enabled
                                get-trades
                                `(fn [markets#]
                                   (map #(assoc % :dry-run true)
                                        (~get-trades markets#))))}
        sym (symbol (str (string/lower-case (str name)) "-strategy"))]
    `(do
       (def ~sym ~strategy)
       (when ~enabled
         (swap! strategy-registry assoc '~name ~strategy))
       ~strategy)))

(defn get-search-params
  "Retrieves market search parameters for a given strategy.
   
   Parameters:
   - strategy: A map containing strategy details.
   
   Returns:
   A list of maps of market search parameters for the strategy."
  [strategy]
  (let [params ((:get-search-params strategy) (config/config-for-strategy (:name strategy)))]
    (if (seq? params) params [params])))

(defn get-trades
  "Generates trades for a given strategy based on market data.
   
   Parameters:
   - strategy: A map containing strategy details.
   - markets: A list of markets to generate trades for.
   
   Returns:
   A list of trades generated by the strategy."
  [strategy markets]
  ((:get-trades strategy) (config/config-for-strategy (:name strategy)) markets))

(defn get-strategies
  "Retrieves all enabled strategies from the strategy registry.
   
   Returns:
   A list of all enabled strategy maps."
  []
  (vals @strategy-registry))

; Strategies and helper functions

(defn kelly-bet-fraction
  "Calculates the Kelly criterion bet fraction.

   Parameters:
   - price: The current price (probability) of a share.
   - true-value: The estimated true value (probability) of a share.
   - market-deferral: (optional) A factor to reduce the bet size, where 0 means full Kelly,
     and higher values reduce the bet size. Default is 0 (full Kelly).

   Returns:
   The recommended bet fraction."
  [price true-value & [market-deferral]]
  (try
    (let [market-deferral (or market-deferral 0)
          p (+ (* true-value (- 1 market-deferral)) (* price market-deferral)) ; probability of winning
          b (/ (- 1 price) price)] ; odds received on the bet
      (max 0 (- p (/ (- 1 p) b))))
    (catch ArithmeticException _
      0)))

(defn kelly-bet-size
  "Calculates the Kelly criterion bet size.

   Parameters:
   - price: The current price (probability) of a share.
   - true-value: The estimated true value (probability) of a share.
   - market-deferral: (optional) A factor to reduce the bet size, where 0 means full Kelly,
     and higher values reduce the bet size. Default is 0 (full Kelly).

   Returns:
   The recommended bet size."
  [price true-value & [market-deferral]]
  (let [current-balance (:balance (api/get-my-user-info))
        kelly-fraction (kelly-bet-fraction price true-value market-deferral)]
    (* kelly-fraction current-balance)))

(defstrategy Null
  :description "Do nothing"
  :get-search-params (fn [config] [])
  :get-trades (fn [config markets] []))

(defstrategy FairlyRandom
  :description "Market make in recurring markets resolved by FairlyRandom"
  :get-search-params (fn [config]
                       (for [market (:markets config)]
                         {:term (:name market)
                          :creatorId (:creatorId market)
                          :filter "open"
                          :contractType "BINARY"}))
  :get-trades (fn [config markets]
                (mapcat (fn [{:keys [id question creatorId] :as market}]
                          (let [market-config (some #(when (and (string/includes? question (:name %)) (= (:creatorId %) creatorId)) %) (:markets config))
                                probability (:probability market-config)
                                yes-price (- probability (:spread config))
                                no-price (+ probability (:spread config))
                                positions (api/get-my-positions id)
                                orders (api/get-my-open-orders id)
                                should-bet? (fn [outcome]
                                              (not (or
                                                    (some-> ((keyword outcome) positions) pos?)
                                                    (some #(= (:outcome %) outcome) orders))))]
                            (remove nil?
                                    [(when (should-bet? "YES")
                                       {:market-id id
                                        :amount (kelly-bet-size yes-price probability)
                                        :outcome "YES"
                                        :limit yes-price})
                                     (when (should-bet? "NO")
                                       {:market-id id
                                        :amount (kelly-bet-size (- 1 no-price) (- 1 probability))
                                        :outcome "NO"
                                        :limit no-price})])))
                        markets)))